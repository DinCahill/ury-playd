# This package installs the playd binary and the man page. It can also build and run the tests.
#
# To build on Arch:
#
# Install base-devel (which includes gcc), clang if you want it, and git.
# e.g. $ pacman -S base-devel clang git
#
# Build with `makepkg`
# Useful options: makepkg -s  # install dependencies automatically
#                         -i  # install package once it's built
#                         -c  # clean up pkg/ and src/ after building
#                         -f  # force build if package already exists
#                  --nocheck  # skip building and running the tests
#
# See `man makepkg` for more

_basename=ury-playd
_realname=${_basename}-git
pkgver=git
pkgrel=1
pkgdesc="Minimal C++ audio player"
url="https://universityradioyork.github.io/ury-playd/"
license=("MIT" "custom")
arch=("i686" "x86_64")

pkgname="${_realname}"

makedepends=("cmake"
             "libuv"
             "mpg123"
             "libsndfile"
             "sdl2")
depends=("libuv"
         "mpg123"
         "libsndfile"
         "sdl2")

provides=("ury-playd")
conflicts=("ury-playd")

source=("ury-playd-git::git+http://github.com/UniversityRadioYork/ury-playd.git#branch=master")
md5sums=("SKIP")

pkgver() {
  cd "${srcdir}/${_realname}"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${_realname}"
  cmake -G "Unix Makefiles" "${srcdir}/${_realname}"
  make playd
}

package() {
  cd "${srcdir}/${_realname}"
  cmake -G "Unix Makefiles" "${srcdir}/${_realname}" \
    -DCMAKE_INSTALL_PREFIX:PATH="${pkgdir}/usr/"
  make install
}

check() {
  cd "${srcdir}/${_realname}"
  cmake -G "Unix Makefiles" "${srcdir}/${_realname}"
  make check
}
